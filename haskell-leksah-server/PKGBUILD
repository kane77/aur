# Maintainer: Martin Harvan <martinhrvn@gmail.com>
_hkgname=leksah-server
pkgname=haskell-leksah-server
pkgver=0.12.0.5
pkgrel=1
pkgdesc="Metadata collection for leksah"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-cabal' 'haskell-binary' 'haskell-binary-shared' 'haskell-bytestring' 'haskell-containers' 'haskell-deepseq' 'haskell-directory' 'haskell-filepath' 'haddock' 'haskell-hslogger' 'haskell-ltk' 'haskell-mtl' 'haskell-network' 'haskell-parsec' 'haskell-pretty' 'haskell-process' 'haskell-time' 'haskell-unix' 'haskell-attoparsec' 'haskell-attoparsec-enumerator' 'haskell-enumerator')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
md5sums=('4a41ab8cddeaef704f73eba3e9f928ed')
